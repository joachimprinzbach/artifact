apply plugin: 'com.moowork.node'

def clientRoot = file("${project.projectDir}/client")
def clientDist = file("${clientRoot.getAbsolutePath()}/dist")

node {
    version = '7.2.0'
    npmVersion = '4.0.3'

    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    workDir = new File(clientRoot, ".gradle/nodejs")

    // Set the work directory where node_modules should be located
    nodeModulesDir = clientRoot
}

task clientBuild(type: NpmTask) {
    group = 'Client'
    description = 'Builds the client'
    workingDir  = clientRoot

    args = ['run', 'build']
    inputs.files fileTree(dir: clientRoot, excludes: ["dist/**", "test/**"])
    outputs.dir(clientDist)
}

task clientArtifacts(type: Copy) {
    from clientDist
    into "build/classes/static"
}

npm_install.dependsOn npmSetup
clientBuild.dependsOn npm_install
clientArtifacts.dependsOn clientBuild
